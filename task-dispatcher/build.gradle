plugins {
    id 'java'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '0.7'
    id 'maven-publish'
}

group 'net.dblsaiko.origami'

apply from: 'https://dblsaiko.net/pub/gradle/autoversion.gradle'

sourceCompatibility = 16
targetCompatibility = 16

tasks.withType(JavaCompile) {
    options.compilerArgs += "--enable-preview"
}

tasks.withType(Test) {
    jvmArgs += "--enable-preview"
}

tasks.withType(JavaExec) {
    jvmArgs += '--enable-preview'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

tasks.withType(Jar) {
    archiveFileName = project.provider {
        var maybe = { String prefix, String value -> value ? prefix ? "-${value}" : value : "" }

        String name = archiveBaseName.getOrElse("")
        name += maybe(name, archiveAppendix.getOrNull())
        name += maybe(name, archiveClassifier.getOrNull())

        String extension = archiveExtension.getOrNull()
        name += extension ? "." + extension : ""
        return name
    }

    manifest {
        attributes('Version': archiveVersion)
    }
}

idea.project.settings {
    delegateActions {
        delegateBuildRunToGradle = false
        testRunner = org.jetbrains.gradle.ext.ActionDelegationConfig.TestRunner.PLATFORM
    }
}

apply from: 'https://dblsaiko.net/pub/gradle/publish-slim.gradle'