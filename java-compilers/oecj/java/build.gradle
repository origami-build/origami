plugins {
    id 'java'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '0.7'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

group 'net.dblsaiko.origami'

apply from: 'https://dblsaiko.net/pub/gradle/autoversion.gradle'

sourceCompatibility = 16
targetCompatibility = 16

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'org.eclipse.jdt', name: 'ecj', version: '3.26.0'
    shadow group: 'org.eclipse.jdt', name: 'ecj', version: '3.26.0'

    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

tasks.withType(Jar) {
    archiveFileName = project.provider {
        var maybe = { String prefix, String value -> value ? prefix ? "-${value}" : value : "" }

        String name = archiveBaseName.getOrElse("")
        name += maybe(name, archiveAppendix.getOrNull())
        name += maybe(name, archiveClassifier.getOrNull())

        String extension = archiveExtension.getOrNull()
        name += extension ? "." + extension : ""
        return name
    }

    manifest {
        attributes('Version': archiveVersion)
    }
}

idea.project.settings {
    delegateActions {
        delegateBuildRunToGradle = false
        testRunner = org.jetbrains.gradle.ext.ActionDelegationConfig.TestRunner.PLATFORM
    }
}

apply from: 'https://dblsaiko.net/pub/gradle/publish-slim.gradle'
